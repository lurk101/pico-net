
.program uart_tx

.side_set 1 opt

; 32n1 UART transmit program.
; OUT pin 0 and side-set pin 0 are both mapped to UART TX pin.

    pull       side 1 [7]  ; Assert stop bit, or stall with line in idle state
    set x, 31  side 0 [7]  ; Preload bit counter, assert start bit for 8 clocks
bitloop:                   ; This loop will run 32 times (32n1 UART)
    out pins, 1            ; Shift 1 bit from OSR to the first OUT pin
    jmp x-- bitloop   [6]  ; Each loop iteration is 8 cycles.

.program uart_rx

; 32n1 UART receiver. Wait for the start bit, then sample 32 bits.
; IN pin 0 is mapped to the GPIO used as UART RX.
; Autopush must be enabled, with a threshold of 32.

    wait 0 pin 0        ; Wait for start bit
    set x, 31 [10]      ; Preload bit counter, delay until eye of first data bit
bitloop:                ; Loop 32 times
    in pins, 1          ; Sample data
    jmp x-- bitloop [6] ; Each iteration is 8 cycles

